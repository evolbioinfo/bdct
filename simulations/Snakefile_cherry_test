import os

# To run locally:
# snakemake --snakefile Snakefile_cherry_test --keep-going --cores 7 --use-singularity --singularity-prefix ~/.singularity --singularity-args "--home ~"


localrules: all

folder = os.path.abspath(config.get("folder", '.'))
sim_folder = os.path.join(folder, 'trees')
n = int(config.get("n", 100))
REPETITIONS = list(range(n))

rule all:
    input:
        os.path.join(sim_folder, 'cherry_tests.tab'),


rule simulate_bd:
    '''
    Simulate a BD tree for the same parameters as the corresponding BDPN tree.
    '''
    input:
        log = os.path.join(sim_folder, 'BDPN', 'tree.{i}.log'),
    output:
        log = os.path.join(sim_folder, 'BD', 'tree.{i}.log'),
        nwk = os.path.join(sim_folder, 'BD', 'tree.{i}.nwk')
    params:
        mem = 2000,
        name = 'simulate_{i}',
        qos = 'fast'
    threads: 1
    singularity: "docker://evolbioinfo/treesimulator:v0.1.2"
    shell:
        """
        R=`tail -n 1 {input.log} | awk -F',' '{{ print $1 }}'`
        p=`tail -n 1 {input.log} | awk -F',' '{{ print $3 }}'`
        it=`tail -n 1 {input.log} | awk -F',' '{{ print $2 }}'`
        psi=`echo "print(1/${{it}})" | python3`
        la=`echo "print($R*${{psi}})" | python3`
        cat {input.log}
        
        while [ ! -f {output.nwk} ]
        do
            timeout 10m generate_bd --min_tips 500 --max_tips 1000 --la $la --psi $psi --p $p --nwk {output.nwk} --log {output.log} || echo 'The tree took too long, restarting!'
        done
        """

rule cherry_test:
    '''
    PN-test on a given tree.
    '''
    input:
        nwk=os.path.join(sim_folder,'{model}','tree.{i}.nwk')
    output:
        log=os.path.join(sim_folder,'{model}','tree.{i}.cherry_test'),
    params:
        mem=2000,
        name='cherry_test_{i}',
        qos='fast'
    threads: 1
    singularity: "docker://evolbioinfo/bdpn:v0.1.2"
    shell:
        """
        pn_test --nwk {input.nwk} --log {output.log} --block_size 100
        """


rule combine:
    '''
    Combine results.
    '''
    input:
        log = expand(os.path.join(sim_folder,'{model}','tree.{i}.cherry_test'), i=REPETITIONS, model=['BD', 'BDPN'])
    output:
        tab = os.path.join(sim_folder, 'cherry_tests.tab'),
    params:
        mem = 2000,
        name = 'combine',
        qos = 'fast',
    threads: 1
    singularity: "docker://evolbioinfo/python-evol:v3.6richer.1"
    shell:
        """
        python3 py/summary_table_cherries.py --logs {input.log} --tab {output.tab}
        """