import os

# To run locally:
# snakemake --snakefile Snakefile_viz --keep-going --cores 7 --use-singularity --singularity-prefix ~/.singularity --singularity-args "--home ~"



localrules: all

os.makedirs('logs', exist_ok=True)

folder = os.path.abspath(config.get("folder", '.'))
sim_folder = os.path.abspath(config.get("sf", os.path.join(folder, 'trees')))
n = int(config.get("n", 100))
REPETITIONS = list(range(n))

rule all:
    input:
        expand(os.path.join(sim_folder, 'BDPN', 'errors_{par}.svg'), par=['psi', 'p']),
        os.path.join(sim_folder,'BDPN','likelihood_stats.txt'),
        stats = os.path.join(sim_folder, 'BDPN', 'CI_stats.txt'),


rule likelihood_stats:
    '''
    Calculate likelihood values for estimated parameters.
    '''
    input:
        tab = os.path.join(sim_folder, 'BDPN', 'estimates.tab'),
        nwk = expand(os.path.join(sim_folder, 'BDPN', 'tree.{i}.nwk'), i=REPETITIONS),
    output:
        tab = os.path.join(sim_folder, 'BDPN', 'loglikelihoods.tab'),
        stats = os.path.join(sim_folder, 'BDPN', 'likelihood_stats.txt'),
    params:
        mem = 2000,
        name = 'likelihoods',
        qos = 'fast',
        nwk = os.path.join(sim_folder, 'BDPN', 'tree.{}.nwk')
    threads: 1
    singularity: "docker://evolbioinfo/bdpn:v0.4"
    shell:
        """
        python3 py/likelihood_comparison.py --estimates {input.tab} \
        --tree_pattern {params.nwk} --likelihoods {output.tab} --log {output.stats}
        """

rule CI_stats:
    '''
    Calculate CI stats for estimated parameters.
    '''
    input:
        tab = os.path.join(sim_folder, 'BDPN', 'estimates.tab'),
    output:
        stats = os.path.join(sim_folder, 'BDPN', 'CI_stats.txt'),
    params:
        mem = 2000,
        name = 'CIs',
        qos = 'fast',
    threads: 1
    singularity: "docker://evolbioinfo/python-evol:v3.6richer.1"
    shell:
        """
        python3 py/assess_CIs.py --estimates {input.tab} --log {output.stats}
        """

rule combine_estimates:
    '''
    Combine estimates.
    '''
    input:
        log = expand(os.path.join(sim_folder, 'BDPN', 'tree.{i}.log'), i=REPETITIONS),
        est_p = expand(os.path.join(sim_folder, 'BDPN', 'tree.{i}.p.est'), i=REPETITIONS),
        est_psi = expand(os.path.join(sim_folder, 'BDPN', 'tree.{i}.psi.est'), i=REPETITIONS),
        est_la = expand(os.path.join(sim_folder, 'BDPN', 'tree.{i}.la.est'), i=REPETITIONS),
        est_p_bd = expand(os.path.join(sim_folder, 'BDPN', 'tree.{i}.p.est_bd'), i=REPETITIONS),
        est_psi_bd = expand(os.path.join(sim_folder, 'BDPN', 'tree.{i}.psi.est_bd'), i=REPETITIONS),
        est_la_bd = expand(os.path.join(sim_folder, 'BDPN', 'tree.{i}.la.est_bd'), i=REPETITIONS),
    output:
        tab = os.path.join(sim_folder, 'BDPN', 'estimates.tab'),
    params:
        mem = 2000,
        name = 'estimates',
        qos = 'fast',
    threads: 1
    singularity: "docker://evolbioinfo/python-evol:v3.6richer.1"
    shell:
        """
        python3 py/summary_table.py --real {input.log} \
        --estimated_p {input.est_p} \
        --estimated_psi {input.est_psi} \
        --estimated_la {input.est_la} \
        --estimated_p_bd {input.est_p_bd} \
        --estimated_psi_bd {input.est_psi_bd} \
        --estimated_la_bd {input.est_la_bd} \
        --tab {output.tab}
        """

rule plot_errors:
    '''
    Plots the errors.
    '''
    input:
        tab = os.path.join(sim_folder, 'BDPN', 'estimates.tab'),
    output:
        pdf = os.path.join(sim_folder, 'BDPN', 'errors_{p}.svg'),
        tab = os.path.join(sim_folder, 'BDPN', 'errors_{p}.tab'),
    params:
        mem = 2000,
        name = 'errors',
        qos = 'fast'
    threads: 1
    singularity: "docker://evolbioinfo/python-evol:v3.6richer.1"
    shell:
        """
        python3 py/plot_error.py --estimates {input.tab} --tab {output.tab} --pdf {output.pdf} --fixed {wildcards.p}
        """

rule plot_errors_pn_small:
    '''
    Plots the errors.
    '''
    input:
        tab = os.path.join(sim_folder, 'BDPN', 'estimates.tab'),
    output:
        pdf = os.path.join(sim_folder, 'BDPN', 'errors_pn<0.5.svg'),
        tab = os.path.join(sim_folder, 'BDPN', 'errors_pn<0.5.tab'),
    params:
        mem = 2000,
        name = 'errors',
        qos = 'fast'
    threads: 1
    singularity: "docker://evolbioinfo/python-evol:v3.6richer.1"
    shell:
        """
        python3 py/plot_error.py --estimates {input.tab} --tab "{output.tab}" --pdf "{output.pdf}" --pn_max 0.1
        """

rule plot_errors_pn_large:
    '''
    Plots the errors.
    '''
    input:
        tab = os.path.join(sim_folder, 'BDPN', 'estimates.tab'),
    output:
        pdf = os.path.join(sim_folder, 'BDPN', 'errors_pn>=0.5.svg'),
        tab = os.path.join(sim_folder, 'BDPN', 'errors_pn>=0.5.tab'),
    params:
        mem = 2000,
        name = 'errors',
        qos = 'fast'
    threads: 1
    singularity: "docker://evolbioinfo/python-evol:v3.6richer.1"
    shell:
        """
        python3 py/plot_error.py --estimates {input.tab} --tab "{output.tab}" --pdf "{output.pdf}" --pn_min 0.1
        """
