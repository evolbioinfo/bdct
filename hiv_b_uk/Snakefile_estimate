import os

# To run locally:
# max_label --use-singularity --singularity-prefix ~/.singularity --singularity-args "--home ~"

# To run on maestro:
# source ~/.bashrc && conda activate snakemake && module load apptainer && cd /pasteur/zeus/projets/p01/Evolbioinfo/users/azhukova/projects/bdct/hiv_b_uk
# snakemake --snakefile Snakefile_estimate --keep-going --cores 1 --use-singularity --singularity-prefix $HOME/.singularity --singularity-args "-B /pasteur" --cluster "sbatch -c {threads} -o logs/{params.name}.log -e logs/{params.name}.log --mem {params.mem} -p common --qos={params.qos}  -A bioevo -J {params.name}" --jobs 300


localrules: all

os.makedirs('logs', exist_ok=True)

data_dir = os.path.abspath(config.get("data", 'data'))
n = int(config.get("n", 10))
REPETITIONS = list(range(n))

rule all:
    input:
        expand(os.path.join(data_dir, 'estimates_{model}.tab'), model=['bd', 'bdct']),
        expand(os.path.join(data_dir, 'forest.{min_date}_{max_date}.{i}.cherry_test'), i=REPETITIONS, min_date=[2012], max_date=[2015]),
        expand(os.path.join(data_dir, 'likelihood_stats.{min_date}_{max_date}.p={p}.txt'), min_date=[2012], max_date=[2015], p=[0.58])


rule estimate_params_p_fixed_bdct:
    '''
    Estimate parameters.
    '''
    input:
        nwk = os.path.join(data_dir, '{forest}.nwk')
    output:
        est = os.path.join(data_dir, '{forest}.p={p}.est_bdct')
    params:
        mem = 2000,
        name = 'est_p',
        qos = 'fast'
    threads: 1
    singularity: "docker://evolbioinfo/bdct:v0.1.23"
    shell:
        """
        bdct_infer --p {wildcards.p}  --nwk {input.nwk} --log {output.est} --ci
        """

rule estimate_params_p_fixed_bd:
    '''
    Estimate parameters.
    '''
    input:
        nwk = os.path.join(data_dir, '{forest}.nwk')
    output:
        est = os.path.join(data_dir, '{forest}.p={p}.est_bd')
    params:
        mem = 2000,
        name = 'est_p',
        qos = 'fast'
    threads: 1
    singularity: "docker://evolbioinfo/bdct:v0.1.23"
    shell:
        """
        bd_infer --p {wildcards.p}  --nwk {input.nwk} --log {output.est} --ci
        """


rule likelihood_bd:
    '''
    Calculate likelihood value for estimated parameters.
    '''
    input:
        log = os.path.join(data_dir, '{forest}.p={p}.est_bd'),
        nwk = os.path.join(data_dir, '{forest}.nwk')
    output:
        lk=os.path.join(data_dir, '{forest}.p={p}.lk_bd'),
    params:
        mem=2000,
        name='lk_bd',
        qos='fast',
    threads: 1
    singularity: "docker://evolbioinfo/bdct:v0.1.23"
    shell:
        """
        R=`head -n 2 {input.log} | tail -n 1 | awk -F',' '{{ print $2 }}'`
        it=`head -n 2 {input.log} | tail -n 1 | awk -F',' '{{ print $3 }}'`
        p=`head -n 2 {input.log} | tail -n 1 | awk -F',' '{{ print $4 }}'`
        psi=`echo "print(1/${{it}})" | python3`
        la=`echo "print($R*${{psi}})" | python3`

        bd_loglikelihood --nwk {input.nwk} --la ${{la}} --psi ${{psi}} --p $p > {output.lk}
        """


rule likelihood_bdct:
    '''
    Calculate likelihood value for estimated parameters.
    '''
    input:
        log = os.path.join(data_dir, '{forest}.p={p}.est_bdct'),
        nwk = os.path.join(data_dir, '{forest}.nwk')
    output:
        lk=os.path.join(data_dir, '{forest}.p={p}.lk_bdct'),
    params:
        mem=2000,
        name='lk_bdct',
        qos='fast',
    threads: 1
    singularity: "docker://evolbioinfo/bdct:v0.1.23"
    shell:
        """
        R=`head -n 2 {input.log} | tail -n 1 | awk -F',' '{{ print $2 }}'`
        it=`head -n 2 {input.log} | tail -n 1 | awk -F',' '{{ print $3 }}'`
        p=`head -n 2 {input.log} | tail -n 1 | awk -F',' '{{ print $4 }}'`
        ups=`head -n 2 {input.log} | tail -n 1 | awk -F',' '{{ print $5 }}'`
        rt=`head -n 2 {input.log} | tail -n 1 | awk -F',' '{{ print $6 }}'`
        psi=`echo "print(1/${{it}})" | python3`
        phi=`echo "print(1/${{rt}})" | python3`
        la=`echo "print($R*${{psi}})" | python3`

        bdct_loglikelihood --nwk {input.nwk} --la ${{la}} --psi ${{psi}} --p $p --upsilon ${{ups}} --phi ${{phi}} > {output.lk}
        """

rule likelihood_stats:
    '''
    Calculate likelihood values for estimated parameters.
    '''
    input:
        lk_bd = expand(os.path.join(data_dir, 'forest.{{min_date}}_{{max_date}}.{i}.p={{p}}.lk_bd'), i=REPETITIONS),
        lk_bdct = expand(os.path.join(data_dir, 'forest.{{min_date}}_{{max_date}}.{i}.p={{p}}.lk_bdct'), i=REPETITIONS),
    output:
        stats = os.path.join(data_dir, 'likelihood_stats.{min_date}_{max_date}.p={p}.txt'),
    params:
        mem = 2000,
        name = 'likelihoods',
        qos = 'fast'
    threads: 1
    singularity: "docker://evolbioinfo/bdct:v0.1.23"
    shell:
        """
        python3 py/likelihood_stats.py --likelihoods_bd {input.lk_bd} --likelihoods_bdct {input.lk_bdct} --log {output.stats}
        """


rule cherry_test:
    '''
    CT-test on a given tree.
    '''
    input:
        nwk = os.path.join(data_dir, '{forest}.nwk')
    output:
        log = os.path.join(data_dir, '{forest}.cherry_test')
    params:
        mem=2000,
        name='ct_test',
        qos='fast'
    threads: 1
    singularity: "docker://evolbioinfo/bdct:v0.1.23"
    shell:
        """
        ct_test --nwk {input.nwk} --log {output.log}
        """


rule combine_estimates_bdct:
    '''
    Combine estimates.
    '''
    input:
        nwk = expand(os.path.join(data_dir, 'forest.{min_date}_{max_date}.{i}.nwk'), i=REPETITIONS,
            min_date=[2012], max_date=[2015]),
        est = expand(os.path.join(data_dir, 'forest.{min_date}_{max_date}.{i}.p={p}.est_bdct'), i=REPETITIONS,
            p=[0.58], min_date=[2012], max_date=[2015]),
    output:
        tab = os.path.join(data_dir, 'estimates_bdct.tab'),
    params:
        mem = 2000,
        name = 'estimates',
        qos = 'fast',
    threads: 1
    singularity: "docker://evolbioinfo/python-evol:v3.6richer.1"
    shell:
        """
        python3 py/summary_table.py --forests {input.nwk} --estimates {input.est} --tab {output.tab} 
        """

rule combine_estimates_bd:
    '''
    Combine estimates.
    '''
    input:
        nwk = expand(os.path.join(data_dir, 'forest.{min_date}_{max_date}.{i}.nwk'), i=REPETITIONS,
            min_date=[2012], max_date=[2015]),
        est = expand(os.path.join(data_dir, 'forest.{min_date}_{max_date}.{i}.p={p}.est_bd'), i=REPETITIONS,
            p=[0.58], min_date=[2012], max_date=[2015]),
    output:
        tab = os.path.join(data_dir, 'estimates_bd.tab'),
    params:
        mem = 2000,
        name = 'estimates',
        qos = 'fast',
    threads: 1
    singularity: "docker://evolbioinfo/python-evol:v3.6richer.1"
    shell:
        """
        python3 py/summary_table_bd.py --forests {input.nwk} --estimates {input.est} --tab {output.tab} 
        """
